buildscript {
    ext {
        springBootVersion = '2.2.10.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


plugins {
    id 'com.palantir.git-version' version '0.12.3'
}


apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.git-version'

def details = versionDetails()

mainClassName = "com.rookout.tutorial.TutorialApplication"

group = 'com.rookout'
version = '1.0.0'
sourceCompatibility = 8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')

    // Specific dependencies for fixing different vulnerabilities
    compile("org.hibernate.validator:hibernate-validator:6.1.3.Final")
    compile("org.springframework:spring-webmvc:5.2.9.RELEASE")
    compile("org.springframework:spring-web:5.2.9.RELEASE")
    compile("org.apache.tomcat.embed:tomcat-embed-core:9.0.40")
    compile("org.yaml:snakeyaml:1.26")
    // tracing support
    compile group: 'io.opentracing.contrib', name: 'opentracing-spring-jaeger-web-starter', version: '3.1.2'
    
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}


class Download extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
       ant.get(src: sourceUrl, dest: target)
    }
}


task downloadRookout(type: Download) {
    sourceUrl = "https://repository.sonatype.org/service/local/artifact/maven/redirect?r=central-proxy&g=com.rookout&a=rook&v=0.1.164"
    target = new File('rook.jar')
}

// bootJar is added by spring + java plugin, it creates a fat jar, we add the source files
// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#reacting-to-other-plugins-java
bootJar {
    manifest {        
        attributes('ROOKOUT_COMMIT': details.gitHashFull)        
    }
    from sourceSets.main.allSource
}

bootJar.dependsOn downloadRookout

run {
    jvmArgs += ["-javaagent:rook.jar"]
}
